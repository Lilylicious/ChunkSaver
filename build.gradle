buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'idea'
apply plugin: 'java'

version = "${minecraft_version}-${staticchunkmanager_version}"
group= "java.lilylicious" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "StaticChunkManager"

minecraft {
    version = "${minecraft_version}-${forge_version}-${minecraft_version}"
    runDir = "eclipse"

    replace "@VERSION@", project.version
    replaceIn "StaticChunkManagerCore.java"
}

dependencies {
    compile "codechicken:CodeChickenCore:1.7.10-1.0.4.29:dev"
    compile "codechicken:CodeChickenLib:1.7.10-1.1.1.110:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.4.83:dev"
    
    // Adds all local libs dependencies in the libs folder
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

repositories {
    maven { //Waila Repo
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { //NEI Repo
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }


        }

// Dev jar (deobfuscated)
task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    version = "${project.version}"
}

artifacts {
    archives devJar
}

idea.module.inheritOutputDirs = true
